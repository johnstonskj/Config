# -*- mode: sh; eval: (sh-set-shell "zsh") -*-

############################################################################
#
# Zsh startup file sequence:
#
# 1. `.zshenv` -- is sourced on all invocations of the shell, unless the
#    `-f` option is set. It should contain commands to set the command
#    search path, plus other important environment variables. `.zshenv`
#    should not contain commands that produce output or assume the shell is
#    attached to a tty.
# 2. `.zprofile` -- is similar to `.zlogin`, except that it is sourced
#    before `.zshrc`. `.zprofile` is meant as an alternative to `.zlogin`
#    for ksh fans; the two are not intended to be used together, although
#    this could certainly be done if desired.
# 3. `.zshrc` -- is sourced in **interactive** shells. It should contain
#    commands to set up aliases, functions, options, key bindings, etc.
# 4. `.zlogin` -- is sourced in login shells. It should contain commands
#    that should be executed only in login shells. `.zlogin` is not the
#    place for alias definitions, options, environment variable settings,
#    etc.; as a general rule, it should not change the shell environment at
#    all. Rather, it should be used to set the terminal type and run a
#    series of external commands (`fortune`, `msgs`, etc).
# 5. `.zlogout` -- is sourced when login shells exit.
#
############################################################################

############################################################################
# Bootstrap
############################################################################

export ZDOTDIR=${ZDOTDIR:-${HOME}}

export XXDG_PROJECT_HOME=${XDG_PROJECT_HOME:-${HOME}/Projects}
export XXDG_LOCAL_ROOT=${HOME}/.local

bootstrap_plugin_load() {
    local plugin_name="${1}"
    local plugin_path="${XXDG_PROJECT_HOME}/zsh-${plugin_name}-plugin/${plugin_name}.plugin.zsh"

    if [[ -f ${plugin_path} ]]; then
        source ${plugin_path}
    else
        echo "Error: shell manager script ${plugin_path} not found."
    fi
}

bootstrap_plugin_load xdg
bootstrap_plugin_load shlog
bootstrap_plugin_load paths

unfunction bootstrap_plugin_load

############################################################################
# Start Here
############################################################################

export SHLOG_LEVEL=1 # Errors only

log_scope_enter "zshenv"

############################################################################
# Basic Environment
############################################################################

log_scope_enter "basics"

export ARCHFLAGS="-arch $(uname -m)"

if [[ -n "${SSH_CONNECTION}" ]]; then
    export EDITOR=vim
else
    export EDITOR='emacsclient -nw'
fi

export PAGER=less

export VISUAL=emacs

log_scope_exit "basics"

############################################################################
# Sessions
############################################################################

# This disables the save/restore mechanism as a whole, at least on macos:
export SHELL_SESSIONS_DISABLE=1

############################################################################
# Top-level paths
############################################################################

log_scope_enter "paths"

path_append_if_exists /usr/local/bin
path_append_if_exists /usr/local/sbin
path_prepend_if_exists ${XXDG_LOCAL_ROOT}/bin

man_path_append_if_exists /usr/local/man

log_scope_exit "paths"

############################################################################
# Configure Zsh directories
############################################################################

log_scope_enter "zsh-dirs"

export ZSH_CACHE_HOME=$(xdg_cache_for zsh)
if [[ ! -d ${ZSH_CACHE_HOME} ]]; then
    mkdir -p ${ZSH_CACHE_HOME}
fi

export ZSH_COMPDUMP=${ZSH_CACHE_HOME}

export ZSH_STATE_HOME=$(xdg_state_for zsh)
if [[ ! -d ${ZSH_STATE_HOME} ]]; then
    mkdir -p ${ZSH_STATE_HOME}
fi

log_scope_exit "zsh-dirs"

log_scope_exit "zshenv"
